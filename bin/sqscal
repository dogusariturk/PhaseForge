#!/bin/csh -f

set wrapfilename = "vasp.wrap"
set maxdepth = 5

while ( $#argv != 0 )
  switch ("$1")
    case "-h":
      cat - <<EOF
sqscal, by Siya Zhu and Doguhan Sariturk

Usage:
  sqscal -e [Element1,Element2,...] -l [Lattice1,Lattice2] -lv [Level] -mlip [MLIP] -model [MLIPmodel version] [-vib] [-sro]

Description:
  - [Element1,Element2,...]  List of element symbols in the system
  - [Lattice1,Lattice2]      Standard CALPHAD crystal structures (e.g., FCC_A1, BCC_A2)
  - [Level]                  Fineness of composition grid
  - [MLIP]                   Type of machine-learning interatomic potential
  - [-vib]                   Include vibrational contribution for endmembers
  - [-sro]                   Apply short-range order correction

Notes:
  - No ab-initio data applied.
  - No inflection detection for mechanically unstable structures.
  - Ternary search with 50 K offset applied for liquid phase.
EOF
      exit 1
      breaksw

    case "-e":
      set elements = "$2"
      shift
      breaksw

    case "-l":
      set lattice = "$2"
      shift
      breaksw

    case "-lv":
      set level = "$2"
      shift
      breaksw

    case "-checkrelax":
      set checkrelaxcutoff = "$2"
      shift
      breaksw

    case "-sro":
      set sro
      breaksw

    case "-vib":
      set vib
      breaksw

    case "-mlip":
      set mlip = "$2"
      shift
      breaksw

    case "-model":
      set model = "$2"
      shift
      breaksw

    default:
      break
  endsw
  shift
end

if ( ! $?elements ) then
  echo "Please use [-e elements] to input the elements. Example: -e Cr,Mo,V"
  exit 1
endif

if ( ! $?lattice ) then
  echo "Please use [-l lattice] to input the lattices. Example: -l FCC_A1,BCC_A2,LIQUID"
  exit 1
endif

if ( ! $?level ) then
  echo "Please use [-lv level] to input the level of SQSs."
  exit 1
endif

echo "Elements in the system: $elements"
echo "Lattices in the system: $lattice"

set element_count = `echo "$elements" | awk -F, '{print NF}'`
set lattice_count = `echo "$lattice" | awk -F, '{print NF}'`
set lattice = `echo $lattice | tr ',' ' '`

echo "Using MLIP: $mlip, model: $model"

foreach phase ($lattice)
  echo "Calculating $phase phase"

  sqs2tdb -cp -sp="$elements" -l="$phase" -lv="$level"
  sqs2tdb -cp -sp="$elements" -l="$phase" -lv="$level"

  cd "$phase"

  if ("$phase" == "LIQUID") then
    set md_command = "MLIPliquid -mlip=$mlip -model=$model -dt=50"

    foreachfile str.out pwd \; runstruct_vasp -nr

    foreach file (`find . -name str.out`)
      set dir = `dirname $file`
      cd $dir
      ternary_search -ll=0.7 -ul=1.3 -eps=0.02 -c="$md_command"
      cd -
    end

    (echo 1,0; echo 2,0) >> terms.in
    sqs2tdb -fit

  else
    if ( ! $?checkrelaxcutoff ) then
      foreachfile str.out pwd \; runstruct_MLIP -mlip=$mlip -model=$model
    else
      foreachfile str.out pwd \; runstruct_MLIP -mlip=$mlip -model=$model -checkrelax=$checkrelaxcutoff
    endif

    if ( $?vib ) then
      foreachfile endmem pwd \; fitfc -si=str_relax.out -ernn=4 -ns=1 -nrr
      foreachfile -d 3 wait \; runstruct_MLIP -mlip=$mlip -model=$model -static
      foreachfile endmem pwd \; fitfc -si=str_relax.out -f -frnn=2 -fn
      foreachfile endmem pwd \; robustrelax_vasp -vib
    endif

    if ( "$phase" == "BCC_A2" || "$phase" == "FCC_A1" || "$phase" == "HCP_A3" ) then
      (echo 1,0; echo 2,0) >> terms.in
    else
      (echo "1,0:1,0"; echo "2,0:1,0") >> terms.in
    endif

    if ( $?sro ) then
      sqs2tdb -fit -sro
    else
      sqs2tdb -fit
    endif
  endif

  cd ..
end

sqs2tdb -tdb -oc